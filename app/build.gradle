apply plugin: 'com.android.application'
apply plugin: 'android-apt'

// 时间
def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

// 打正式包命令 gradlew assembleRelease
// 打debug包命令 gradlew assembleDebug
android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "com.brian.csdnblog"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 75
        versionName "1.3.75"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    signingConfigs {
        debug {
            storeFile file("../release/debug.keystore")
        }
        release {
            storeFile file("../release/brian.keystore")
            storePassword 'brian512'
            keyAlias '年少的风'
            keyPassword 'brian512'
        }
    }
    buildTypes {
        // 打测试包
        debug{
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
//            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        // 打正式包
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            // 启用混淆
            minifyEnabled false
            // Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 签名
            signingConfig signingConfigs.release

            // 修改包名
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为CodeBlog_v1.0_2016-07-31_wandoujia.apk
                        def fileName = "CodeBlog_${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk" //_${releaseTime()}
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    //这个是解决lint报错的代码
    lintOptions {
        abortOnError false
        // 防止在发布的时候出现因MissingTranslation导致Build Failed!
        disable 'MissingTranslation'
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            res.srcDirs = ['src/main/res', 'src/main/res/anim']
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
        preDexLibraries = false
    }

    aaptOptions {
        noCompress 'foo', 'bar'
        ignoreAssetsPattern "!.svn:!.git:!.ds_store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~"
    }

    lintOptions {
        abortOnError false
    }

    // 配置渠道包
    productFlavors {
        WANDOUJIA {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "WANDOUJIA", DEBUG_ENABLE: false]
        }

        BAIDU {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "BAIDU", DEBUG_ENABLE: false]
        }

        QIHU360 {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "QIHU360", DEBUG_ENABLE: false]
        }

        XIAOMI {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "XIAOMI", DEBUG_ENABLE: false]
        }

        YINGYONGBAO {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "YINGYONGBAO", DEBUG_ENABLE: false]
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.tencent.bugly:crashreport:latest.release'
    compile 'me.imid.swipebacklayout.lib:library:1.0.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okio:okio:1.7.0'
    compile 'com.squareup.wire:wire-runtime:2.1.2'

    compile 'com.jakewharton:butterknife:8.3.0'
    apt 'com.jakewharton:butterknife-compiler:8.3.0'

    compile 'com.android.support:design:24.2.1'
    compile project(':PushSDK')
}